
/*
 * Copyright Â© 2002 Sun Microsystems, Inc., 4150 Network Circle, Santa Clara,
 * California 95054, U.S.A. All rights reserved.  Sun Microsystems, Inc. has
 * intellectual property rights relating to technology embodied in the product
 * that is described in this document. In particular, and without limitation,
 * these intellectual property rights may include one or more of the U.S.
 * patents listed at http://www.sun.com/patents and one or more additional
 * patents or pending patent applications in the U.S. and in other countries.
 * U.S. Government Rights - Commercial software. Government users are subject
 * to the Sun Microsystems, Inc. standard license agreement and applicable
 * provisions of the FAR and its supplements.  Use is subject to license terms.
 * Sun,  Sun Microsystems,  the Sun logo and  Java are trademarks or registered
 * trademarks of Sun Microsystems, Inc. in the U.S. and other countries.  This
 * product is covered and controlled by U.S. Export Control laws and may be
 * subject to the export or import laws in other countries.  Nuclear, missile,
 * chemical biological weapons or nuclear maritime end uses or end users,
 * whether direct or indirect, are strictly prohibited.  Export or reexport
 * to countries subject to U.S. embargo or to entities identified on U.S.
 * export exclusion lists, including, but not limited to, the denied persons
 * and specially designated nationals lists is strictly prohibited.
 */

options {
  //JAVA_UNICODE_ESCAPE = false;
  //UNICODE_INPUT = false;
  //ERROR_REPORTING = false;
  //DEBUG_PARSER = true;
  STATIC = false;
}

PARSER_BEGIN(JavaParser)

package beauty.parsers.java;

import java.io.*;
import java.util.*;

/**
 * Grammar to parse Java version 1.5
 * @author Sreenivasa Viswanadha - Simplified and enhanced for 1.5
 * @author Dale Anson, some updates for this and that
 *
 * TODO: look at whitespace handling in the jsp beautifier and port the ideas
 * here.  In particular, look at the special token handling and getSpecial(Token).
 */
public class JavaParser
{
    Token t;

    // shouldn't use this, a specific line separator should be set based on
    // buffer settings.  Of course, it may be the same as what the buffer
    // uses anyway.
    String lineSep = System.getProperty("line.separator");

    // one of these is inserted at every place in a line that qualifies as a
    // good place to break a line if it needs wrapped.  You can't type one of
    // these from the keyboard,
    char wrapSep = '\u001c';
    
    // a non-printable, non-typeable character to represent a non-breaking space
    char nbsp = '\u001d';
    
    // bracket styles
    public static final int ATTACHED = 1;
    public static final int BROKEN = 2;
    
    private int bracketStyle = ATTACHED;
    
    private boolean padParens = false;

    public void setIndentWidth(int i) {
        token_source.setIndentWidth(i);
    }

    public void setTabSize(int size) {
        jj_input_stream.setTabSize(size);
    }

    public int getTabSize() {
        // this really isn't necessary for this beautifier.  Setting the tab
        // size on the input stream makes the token locations more accurate
        // is all.
        return jj_input_stream.getTabSize(0);
    }

    public void setUseSoftTabs(boolean b) {
        token_source.setUseSoftTabs(b);
    }

    public String getText() {
        return token_source.getText();
    }

    public void resetTokenSource() {
        token_source.reset();
    }

    private void add(Token t) {
        token_source.add(t);
    }

    private void add(String s) {
        token_source.add(s);
    }
     
    private void addStartBracket() {
        switch(bracketStyle) {
            case BROKEN:
                add(lineSep);
                writeln();
                add("{");
                break;
            case ATTACHED:
            default:
                add(" {");
                break;
        }
    }
    
    private void addEndBracket() {
        add("}");
    }

    private void trim() {
        token_source.trim();
    }

    private void trimWhitespace() {
        token_source.trimWhitespace();
    }

    private void write() {
        token_source.write();
    }

    private void writeln() {
        token_source.writeln();
    }

    public void setLineSeparator(String le) {
        lineSep = le;
        token_source.setLineSeparator(le);
    }
    
    public void setBracketStyle(int style) {
        switch(style) {
            case ATTACHED:
            case BROKEN:
                bracketStyle = style;
                token_source.setBracketStyle(style);
                break;
        }
    }
    
    public void setPadParens(boolean pad) {
        padParens = pad; 
        token_source.setPadParens(pad);
    }

   public JavaParser(String fileName)
   {
      this(System.in);
      try { ReInit(new FileInputStream(new File(fileName))); }
      catch(Exception e) { e.printStackTrace(); }
   }
   
   public void parse() throws ParseException {
       CompilationUnit();   
   }

    public static void main(String args[]) {
        JavaParser parser;
        if (args.length == 0) {
            System.out.println("Java Parser Version 1.1:  Reading from standard input . . .");
            parser = new JavaParser(System.in);
        } else{
            if (args.length == 1) {
                System.out.println("Java Parser Version 1.1:  Reading from file " + args[0] + " . . .");
                try {
                    parser = new JavaParser(new java.io.FileInputStream(args[0]));
                } catch (java.io.FileNotFoundException e) {
                    System.out.println("Java Parser Version 1.1:  File " + args[0] + " not found.");
                    return;
                }
            } else {
                System.out.println("Java Parser Version 1.1:  Usage is one of:");
                System.out.println("         java JavaParser < inputfile");
                System.out.println("OR");
                System.out.println("         java JavaParser inputfile");
                return;
            }
        }

        try {
            parser.CompilationUnit();
            System.out.println("Java Parser Version 1.1:  Java program parsed successfully.");
        } catch (ParseException e) {
            System.out.println(e.getMessage());
            System.out.println("Java Parser Version 1.1:  Encountered errors during parse.");
        }
    }

}

PARSER_END(JavaParser)

/*
    A lot of work happens in the token manager.  All code in this section is
    added to a file named JavaParserTokenManager.java.  Some accessor are
    duplicated in the PARSER section above to make it a little easier to write
    code for the productions.
*/
TOKEN_MGR_DECLS :
{

    // line buffer, text is accumulated here, then written to the output stream
    // on end of line marker.
    static StringBuilder b = new StringBuilder();

    // all text is accumulated here.  When processing is complete, this buffer
    // will contain the final beautified text.
    static StringBuilder outputBuffer = new StringBuilder();

    // accumulate pieces a token or string at a time.  The objects in this array
    // will be ocnverted to strings, padded as appropriate, and added to the
    // line buffer b.  This is the "accumulator".
    static ArrayList a = new ArrayList();

    // where to write the completely beautified code.
    private static PrintWriter out = null;

    // level of indentation
    static int level = 0;

    // width of indent
    static int indent_width = 4;
    static String indent = "    ";
    static String double_indent = indent + indent;

    // the soft tab setting from jEdit, use soft tabs by default.
    static boolean useSoftTabs = true;

    // line separator, defaults to system line separator, but can be set to
    // a specific separator
    static String ls = System.getProperty("line.separator");
    
    // a non-printable, non-typeable character to represent a non-breaking space
    static char nbsp = '\u001d';

    static int bracketStyle = JavaParser.ATTACHED;
    
    static boolean padParens = false;

    static void reset() {
        b = new StringBuilder();
        outputBuffer = new StringBuilder();
        a.clear();
        level = 0;
    }

    static String getText() {
        return outputBuffer.toString();
    }

    static void setLineSeparator(String le) {
        ls = le;
    }
    
    static void setBracketStyle(int style) {
        bracketStyle = style;   
    }

    static void setIndentWidth(int w) {
        indent_width = w;
        if (indent_width <= 0) {
            indent_width = 4;
        }
        StringBuilder sb = new StringBuilder();
        for (int i = 0; i < w; i++) {
            sb.append(' ');
        }
        indent = sb.toString();
        double_indent = indent + indent;
    }

    static void setUseSoftTabs(boolean b) {
        useSoftTabs = b;
        if (b) {
            setIndentWidth(indent_width);
        }
        else {
            indent = "\t";
            double_indent = "\t\t";
        }
    }
    
    static void setPadParens(boolean pad) {
        padParens = pad;   
    }

    // add a token to the accumulator
    static void add(Token t) {
        if (t != null) {
            a.add(t);
        }
    }

    // add a string to the accumulator
    static void add(String s) {
        if (s != null) {
            a.add(s);
        }
    }

    // trim spaces from the last item in the accumulator
    static void trim() {
        if (a.size() == 0)
            return;
        Object o = a.get(a.size() - 1);
        StringBuilder sb = new StringBuilder();
        if (o instanceof Token)
            sb.append( ((Token)o).image );
        else
            sb.append((String)o);
        while(sb.length() > 0 && sb.charAt(sb.length() - 1) == ' ')
            sb.deleteCharAt(sb.length() - 1);
        a.set(a.size() - 1, sb.toString() );
    }

    // trim a single new line from the end of the output buffer
    // return true if a line ender was removed
    static boolean trimNL() {
        boolean trimmed = false;
        if(outputBuffer.length() > 0 && outputBuffer.charAt(outputBuffer.length() - 1) == '\n') {
            outputBuffer.deleteCharAt(outputBuffer.length() - 1);
            trimmed = true;
        }
        if(outputBuffer.length() > 0 && outputBuffer.charAt(outputBuffer.length() - 1) == '\r') {
            outputBuffer.deleteCharAt(outputBuffer.length() - 1);
            trimmed = true;
        }
        return trimmed;
    }

    // trim all \n and/or \r from the end of the given string
    static void trimNL(String s) {
        StringBuilder sb = new StringBuilder(s);
        while(sb.length() > 0 && (sb.charAt(sb.length() - 1) == '\r' || sb.charAt(sb.length() - 1) == '\n'))
            sb.deleteCharAt(sb.length() - 1);
    }

    // trim all whitespace (\r, \n, space, \t) from the start of the given string
    static String trimStart(String s) {
        StringBuilder sb = new StringBuilder(s);
        while(sb.length() > 0 && (sb.charAt(0) == '\r'
                || sb.charAt(0) == '\n'
                || sb.charAt(0) == '\t'
                || sb.charAt(0) == ' ')) {
            sb.deleteCharAt(0);
        }
        return sb.toString();
    }

    // trim up to max whitespace (\r, \n, space, \t) from the start of the given string
    static String trimStart(String s, int max) {
        StringBuilder sb = new StringBuilder(s);
        int trimmed = 0;
        while(sb.length() > 0 && Character.isWhitespace(sb.charAt(0)) && trimmed < max) {
            sb.deleteCharAt(0);
            ++trimmed;
        }
        return sb.toString();
    }

    // trims whitespace (\r, \n, space, \t) from the last items in the
    // accumulator.  If the last item is all whitespace, continues on to the
    // previous until a non-whitespace character is encountered.  If the
    // entire accumulator is whitespace, continues to trim whitespace from the
    // outputBuffer.
    static void trimWhitespace() {
        for (int i = a.size() - 1; i >= 0; i-- ) {
            Object o = a.get(i);
            StringBuilder sb = new StringBuilder();
            if (o instanceof Token)
                sb.append( ((Token)o).image );
            else
                sb.append((String)o);
            while(sb.length() > 0 && (sb.charAt(sb.length() - 1) == '\r'
                    || sb.charAt(sb.length() - 1) == '\n'
                    || sb.charAt(sb.length() - 1) == '\t'
                    || sb.charAt(sb.length() - 1) == ' ')) {
                sb.deleteCharAt(sb.length() - 1);
            }
            if (sb.length() == 0) {
                a.remove(i);
            }
            else {
                a.set(i, sb.toString());
                break;
            }
        }
        if (a.size() == 0) {
            while(outputBuffer.length() > 0 && (outputBuffer.charAt(outputBuffer.length() - 1) == '\r'
                    || outputBuffer.charAt(outputBuffer.length() - 1) == '\n'
                    || outputBuffer.charAt(outputBuffer.length() - 1) == '\t'
                    || outputBuffer.charAt(outputBuffer.length() - 1) == ' ')) {
                outputBuffer.deleteCharAt(outputBuffer.length() - 1);
            }
        }
    }

    // writes the contents of the accumulator to the outputBuffer.  The line
    // buffer (b) is used to build the line.
    static void write() {
        try {
            b.setLength(0); // clear the line buffer

            // this next section builds the output string while protecting
            // string literals.  All extra spaces are removed from the output
            // string, except that string literals are left as is.
            ArrayList list = new ArrayList();
            String s = new String("");
            for (int i = 0; i < a.size(); i++) {
                Object o = a.get(i);
                if (o instanceof Token) {
                    Token token = (Token)o;
                    if (token.kind == JavaParserConstants.STRING_LITERAL) {
                        s = s.replaceAll("[ ]+", " ");
                        list.add(s);
                        s = new String("");
                        list.add(token.image);
                    }
                    else {
                        s += ((Token)o).image;
                        s = s.replaceAll("[ ]+", " ");
                    }
                }
                else {
                    s += (String)o;
                    // replace all non-breaking space chars with actual spaces
                    s = s.replaceAll("[\u001d]", " ");
                    s = s.replaceAll("[ ]+", " ");
                }
            }
            for (int i = 0; i < list.size(); i++) {
                b.append((String)list.get(i));
            }

            b.append(s);
            s = b.toString();


            // check for blank line(s)
            String maybe_blank = new String(s);
            if (maybe_blank.trim().length() == 0) {
                // yep, it's a blank, so just print it out
                if (s.length() >= ls.length()) {
                    s = s.substring(0, s.length() - ls.length());
                }
                s = s.replaceAll(" ", "");
                s = s.replaceAll("\t", "");
                outputBuffer.append(s);
                a.clear();
                return;
            }

            // indent --
            // most lines get indented, but there are a few special cases:
            // "else" gets put on the same line as the closing "}" for the "if",
            // so don't want to indent.  Similarly with "catch" and "finally".
            // The "while" at the end of a "do" loop is marked as "^while" to
            // differentiate it from a regular "while" block. "else if" is also
            // a special case.
            if (!s.startsWith(" else")
                    && !s.startsWith(" catch")
                    && !s.startsWith(" finally")
                    && !s.startsWith(" ^while")
                    && !s.startsWith(" {")
                    && (!endsWith(outputBuffer, "else") && !endsWith(outputBuffer, "else "))) {
                s = s.trim();
                for (int i = 0; i < level; i++) {
                    s = indent + s;
                }
            }

            // maybe clean out the ^ from the specially marked "while" at the
            // end of a "do" loop
            if (s.startsWith(" ^while")) {
                b.deleteCharAt(1);
                s = b.toString();
            }

            // check if the output buffer does NOT end with a new line.  If it
            // doesn't, remove any leading whitespace from this line
            if (!endsWith(outputBuffer, "\n") && !endsWith(outputBuffer, "\r")) {
                s = trimStart(s);
            }

            // check that there aren't extra spaces in the buffer already --
            // this handles the case where the output buffer ends with a space
            // and the new string starts with a space, don't want 2 spaces.
            if (s.startsWith(" ") && endsWith(outputBuffer, " ")) {
                s = s.substring(1);
            }

            // check that there is one space between the end of the output
            // buffer and this line -- this handles the case where the output
            // buffer does not end in a space and the new string does not start
            // with a space, want one space in between.
            if (!s.startsWith(" ")
                    && !endsWith(outputBuffer, " ")
                    && !endsWith(outputBuffer, "\r")
                    && !endsWith(outputBuffer, "\n")
                    && outputBuffer.length() > 0) {
                outputBuffer.append(" ");
            }

            // by the Sun standard, there is no situation where '(' is followed
            // by a space or ')' is preceded with by a space
            // TODO: make this a user setting, pad parens
            String startParen = padParens ? "( " : "(";
            String endParen = padParens ? " )" : ")";
            s = s.replaceAll("[(][ ]", startParen);
            s = s.replaceAll("[ ][)]", endParen);
            s = s.replaceAll("[(]\\s+[)]", "()");
            
            // there should be no situation where a comma is preceded by a space,
            // although that seems to happen when formatting string arrays.
            s = s.replaceAll("\\s+[,]", ",");
            
            // finally! add the string to the output buffer
            // check for line length, may need to wrap.  Sun says to avoid lines
            // longer than 80 characters.  This doesn't work well yet, so I've 
            // commented out the wrapping code.  Still need to clean out the
            // wrapping markers.
            //s = s.replaceAll("[\u001c]", "");
            outputBuffer.append(s);
            /*
            int wrap_sep_count = countWrapSep(s);
            if (s.length() - wrap_sep_count > 80) {
                String[] lines = wrapLines(s);
                if ( lines != null ) {
                    for (int i = 0; i < lines.length; i++) {
                        outputBuffer.append(lines[i]).append(ls);
                    }
                }
                else {
                    // whack any remaining \u001c characters
                    s = s.replaceAll("[\u001c]", "");
                    outputBuffer.append(s);
                }
            }
            else {
                // whack any remaining \u001c characters
                s = s.replaceAll("[\u001c]", "");
                outputBuffer.append(s);
            }
            */
            // clear the accumulator for the next line
            a.clear();
        }
        catch(Exception e) {
            e.printStackTrace();
        }
    }

    static void writeln() {
        write();
        trimNL();
        outputBuffer.append(ls);
    }

    static int countWrapSep(String s) {
        int count = 0;
        for (int i = 0; i < s.length(); i++) {
            if (s.charAt(i) == '\u001c') {
                ++count;
            }
        }
        return count;
    }

    // needs work, does a wrap, but not per spec
    static String[] wrapLines(String s) {
        if (s.length() <= 80) {
            return new String[]{s};
        }
        int wc = countWrapSep(s);
        if (wc > 0) {
            int[] break_points = new int[wc];
            int offset = 0;
            for (int i = 0; i < wc; i++) {
                int index = s.indexOf('\u001c', offset);
                break_points[i] = index;
                offset = index + 1;
            }

            int first_break = -1;
            for (int i = 0; i < break_points.length; i++) {
                int possible = break_points[i];
                if (possible > 80) {
                    break;
                }
                first_break = possible;
            }
            if ( first_break == -1 ) {
                first_break = s.length();
            }

            int ws_length = 0;
            for (int i = 0; i < s.length(); i++) {
                if (s.charAt(i) == ' ')
                    ++ws_length;
                else
                    break;
            }
            String leading_ws = s.substring(0, ws_length);
            String head = s.substring(0, first_break);
            String tail = s.substring(first_break);
            //head = head.replaceAll("[\u001c]", "");
            //tail = tail.replaceAll("[\u001c]", "");
            return new String[]{head, leading_ws + double_indent + tail};
        }
        return null;
    }

    // StringBuilder doesn't have an "endsWith" method
    static boolean endsWith(StringBuilder sb, String s) {
        if (sb == null && s == null)
            return true;
        if (sb == null && s != null)
            return false;
        if (sb.length() < s.length())
            return false;
        String end = sb.substring(sb.length() - s.length());
        return end.equals(s);
    }
    
    static void writeJavadocComment(String s) {
        String[] lines = s.split(ls);
        
        // indent the first line.  It won't have any leading whitespace, but
        // may have trailing whitespace
        String line = lines[0].trim();
        for (int j = 0; j < level; j++) {
            line = indent + line;       // 4 spaces
        }
        outputBuffer.append(line);
        
        // handle the remaining lines, put stars in front of them.
        // TODO: this needs work.  Need to preserve whitespace after
        // the star.
        for (int i = 1; i < lines.length; i++) {
            outputBuffer.append(ls);
            line = lines[i].trim();
            
            // apply padding. All javadoc lines start with a *.
            if (line.startsWith("*")) {
                line = " " + line;
            }
            else {
                line = " * " + line;   
            }

            // apply indenting. The Sun rule is 4 spaces.
            for (int j = 0; j < level; j++) {
                line = indent + line;
            }
            
            outputBuffer.append(line);
        }
    }

    // comments of the /* ... */ variety.  This sort of comment is commonly
    // used to "comment out" a block of code, so I don't want to modify the
    // existing indenting within the block.
    static void writeBlockComment(String s) {
        String[] lines = s.split(ls);

        // indent the first line.  It won't have any leading whitespace, but
        // may have trailing whitespace
        String line = lines[0].trim();
        for (int j = 0; j < level; j++) {
            line = indent + line;
        }
        outputBuffer.append(line);
        
        // output body of comment without change
        for (int i = 1; i < lines.length - 1; i++) {
            outputBuffer.append(ls);
            line = lines[i];    // trimStart(lines[i], level * 4);
            outputBuffer.append(line);
        }
        
        // output the last line.  It will probably have leading whitespace, so
        // trim it then indent it the same as the first line.
        if (lines.length > 1) {
            outputBuffer.append(ls);
            line = lines[lines.length - 1].trim();
            for (int j = 0; j < level; j++) {
                line = indent + line;
            }
            outputBuffer.append(line);
        }
    }
    

    // handle comments like this one
    static void writeEndOfLineComment(String s) {
        String line = s.trim();
        if (line.startsWith("//")) {
            line = line.substring(2).trim();
            line = "// " + line;
        }
        for (int j = 0; j < level; j++) {
            line = indent + line;
        }
        outputBuffer.append(line).append(ls);
    }

}


/********************************************
 * THE JAVA TOKEN SPECIFICATION STARTS HERE *
 ********************************************/


/* WHITE SPACE */

SKIP :
{
    "\f"  // are these EVER used??
}


// TODO: this is the wrong way to handle these.  The TokenManager may be way
// ahead of the parser, which can cause these tokens to be inserted in the
// output in the wrong place.  See the jsp parser for a better way to do this.
SPECIAL_TOKEN :
{
  <LINE_ENDERS: ("\n"|"\r"|"\r\n")+ >
  {
    String s = matchedToken.image;
    int n = 0;
    int r = 0;
    for (int i = 0; i < s.length(); i++) {
        if (s.charAt(i) == 'n')
            ++n;
        else
            ++r;
    }
    int cnt = Math.max(n, r);
    cnt = Math.min(2, cnt);
    if (cnt > 0) {
        for ( ; cnt >= 0; cnt--)
            add(ls);
        write();
    }
  }
| <BLANK_LINES: (" " | "\t")* ("\n"|"\r"|"\r\n")+ >
  {
    String b = matchedToken.image;
    b = b.replaceAll(" ", "");
    b = b.replaceAll("\t", "");
    int bn = 0;
    int br = 0;
    for (int i = 0; i < b.length(); i++) {
        if (b.charAt(i) == 'n')
            ++bn;
        else
            ++br;
    }
    int bcnt = Math.max(bn, br);
    bcnt = Math.min(2, bcnt);
    if (bcnt > 0) {
        for ( ; bcnt >= 0; bcnt--)
            add(ls);
        write();
    }
  }
| <SPACES: ([" "])+ >  { add(matchedToken); }
| <TABS: (["\t"])+ >  { for (int i = 0; i < matchedToken.image.length() * 8; i++) add(" "); }
}


/* COMMENTS */

SPECIAL_TOKEN :
{
  <END_OF_LINE_COMMENT: "//"  (~["\n","\r"])* ("\n"|"\r"|"\r\n")>  { writeEndOfLineComment(matchedToken.image); }
| <JAVADOC_COMMENT:     "/**" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">  { writeJavadocComment(matchedToken.image); }
| <BLOCK_COMMENT:       "/*"  (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">  { writeBlockComment(matchedToken.image); }
}


/* RESERVED WORDS AND LITERALS */

TOKEN :
{
  < ABSTRACT: "abstract" >
| < ASSERT: "assert" >
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CATCH: "catch" >
| < CHAR: "char" >
| < CLASS: "class" >
| < CONST: "const" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < ENUM: "enum" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINAL: "final" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < GOTO: "goto" >
| < IF: "if" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < INSTANCEOF: "instanceof" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NEW: "new" >
| < NULL: "null" >
| < PACKAGE: "package">
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < SHORT: "short" >
| < STATIC: "static" >
| < STRICTFP: "strictfp" >
| < SUPER: "super" >
| < SWITCH: "switch" >
| < SYNCHRONIZED: "synchronized" >
| < THIS: "this" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRANSIENT: "transient" >
| < TRUE: "true" >
| < TRY: "try" >
| < VOID: "void" >
| < VOLATILE: "volatile" >
| < WHILE: "while" >
}

/* LITERALS */

TOKEN :
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|

  <CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["u"] ["0"-"9","a"-"f"] ["0"-"9","a"-"f"] ["0"-"9","a"-"f"] ["0"-"9","a"-"f"]
            | ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  <STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

/* IDENTIFIERS */

TOKEN :
{
  < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|
  < #LETTER:
      [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff",
       "\u0100"-"\u1fff",
       "\u3040"-"\u318f",
       "\u3300"-"\u337f",
       "\u3400"-"\u3d2d",
       "\u4e00"-"\u9fff",
       "\uf900"-"\ufaff"
      ]
  >
|
  < #DIGIT:
      [
       "\u0030"-"\u0039",
       "\u0660"-"\u0669",
       "\u06f0"-"\u06f9",
       "\u0966"-"\u096f",
       "\u09e6"-"\u09ef",
       "\u0a66"-"\u0a6f",
       "\u0ae6"-"\u0aef",
       "\u0b66"-"\u0b6f",
       "\u0be7"-"\u0bef",
       "\u0c66"-"\u0c6f",
       "\u0ce6"-"\u0cef",
       "\u0d66"-"\u0d6f",
       "\u0e50"-"\u0e59",
       "\u0ed0"-"\u0ed9",
       "\u1040"-"\u1049"
      ]
  >
}

/* SEPARATORS */

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < AT: "@" >
}

/* OPERATORS */

TOKEN :
{
  < ASSIGN: "=" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < ELLIPSIS: "..." >
}

/* >'s need special attention due to generics syntax. */
TOKEN :
{
  < RUNSIGNEDSHIFT: ">>>" >
  {
     matchedToken.kind = GT;
     ((Token.GTToken)matchedToken).realKind = RUNSIGNEDSHIFT;
     input_stream.backup(2);
  }
| < RSIGNEDSHIFT: ">>" >
  {
     matchedToken.kind = GT;
     ((Token.GTToken)matchedToken).realKind = RSIGNEDSHIFT;
     input_stream.backup(1);
  }
| < GT: ">" >
}


/*****************************************
 * THE JAVA LANGUAGE GRAMMAR STARTS HERE *
 *****************************************/

/*
 * Program structuring syntax follows.
 */

/** 
 * This is the main entry  point of the parser. In general, a full compilation
 * is expected, however, a block statement is also acceptable.  This means that
 * this beautifier can be used to clean up random bits of java code, like
 * perhaps code in a scriptlet section in a jsp file.  A nice enhancement would
 * be to be able to support beanshell code.
 */
void CompilationUnit():
{}
{
    LOOKAHEAD(3)
    (
        BlockStatement()
    )
    |
    (
      [ PackageDeclaration() ]
      ( ImportDeclaration() )*
      ( TypeDeclaration() )*
    )
  <EOF>
}

void PackageDeclaration():
{}
{
    t="package" { add(t); } Name() ";" { add(";"); write(); }
}

void ImportDeclaration():
{}
{
  "import" { add("import"); } [ "static" { add("static"); } ] Name() [ "." "*" { add(".*"); } ] ";" { add(";"); write(); }
}

/*
 * Modifiers. We match all modifiers in a single rule to reduce the chances of
 * syntax errors for simple modifier mistakes. It will also enable us to give
 * better error messages.
 */

int Modifiers():
{
   int modifiers = 0;
}
{
    try {
 (
  LOOKAHEAD(2)
  (
   "public" { modifiers |= ModifierSet.PUBLIC; }
  |
   "static" { modifiers |= ModifierSet.STATIC; }
  |
   "protected" { modifiers |= ModifierSet.PROTECTED; }
  |
   "private" { modifiers |= ModifierSet.PRIVATE; }
  |
   "final" { modifiers |= ModifierSet.FINAL; }
  |
   "abstract" { modifiers |= ModifierSet.ABSTRACT; }
  |
   "synchronized" { modifiers |= ModifierSet.SYNCHRONIZED; }
  |
   "native" { modifiers |= ModifierSet.NATIVE; }
  |
   "transient" { modifiers |= ModifierSet.TRANSIENT; }
  |
   "volatile" { modifiers |= ModifierSet.VOLATILE; }
  |
   "strictfp" { modifiers |= ModifierSet.STRICTFP; }
  |
   Annotation()
  )
 )*
    }
    catch(ParseException pe) {
        return 0;   
    }
   
 {
    return modifiers;
 }
}

/*
 * Declaration syntax follows.
 */
void TypeDeclaration():
{
   int modifiers;
}
{
    ";" { add(";"); write(); }
|
  modifiers = Modifiers()
  (
     ClassOrInterfaceDeclaration(modifiers)
   |
     EnumDeclaration(modifiers)
   |
     AnnotationTypeDeclaration(modifiers)
  )
}


void ClassOrInterfaceDeclaration(int modifiers):
{
   boolean isInterface = false;
   add(ModifierSet.toString(modifiers) + " ");
}
{
  ( "class" { add("class"); } | "interface" { add("interface");  isInterface = true; } )
  t=<IDENTIFIER> { add(t); }
  [ TypeParameters() ]
  [ ExtendsList(isInterface) ]
  [ ImplementsList(isInterface) ]
  ClassOrInterfaceBody(isInterface)
}

void ExtendsList(boolean isInterface):
{
   boolean extendsMoreThanOne = false;
}
{
   "extends" { add("extends"); } ClassOrInterfaceType()
   ( "," { add(", "); } ClassOrInterfaceType() { extendsMoreThanOne = true; } )*
   {
      if (extendsMoreThanOne && !isInterface)
         throw new ParseException("A class cannot extend more than one other class");
   }
}

void ImplementsList(boolean isInterface):
{}
{
   "implements" { add("implements"); } ClassOrInterfaceType()
   ( "," { add(", "); } ClassOrInterfaceType() )*
   {
      if (isInterface)
         throw new ParseException("An interface cannot implement other interfaces");
   }
}

void EnumDeclaration(int modifiers):
{
    add(ModifierSet.toString(modifiers) + " ");
}
{

  "enum" { add("enum"); } t=<IDENTIFIER> { add(t); }
  [ ImplementsList(false) ]
  EnumBody()
}

void EnumBody():
{}
{
    "{" { addStartBracket(); }
   EnumConstant() ( "," { add(", "); } EnumConstant() )*
   [ ";" { add(";"); write(); } ( ClassOrInterfaceBodyDeclaration(false) )* ]
   "}" { addEndBracket(); write(); }
}

void EnumConstant():
{}
{
  t=<IDENTIFIER> { add(t); } [ Arguments() ] [ ClassOrInterfaceBody(false) ]
}

void TypeParameters():
{}
{
    "<" { trim(); add("<"); } TypeParameter() ( "," { add(", "); } TypeParameter() )* ">" { add("> "); }
}

void TypeParameter():
{}
{
    t=<IDENTIFIER> { add(t); } [ TypeBound() ]
}

void TypeBound():
{}
{
    "extends" { add("extends"); } ClassOrInterfaceType() ( "&" { add("&"); } ClassOrInterfaceType() )*
}

void ClassOrInterfaceBody(boolean isInterface):
{}
{
    "{" { addStartBracket(); write(); ++token_source.level; } ( ClassOrInterfaceBodyDeclaration(isInterface) )* "}" { addEndBracket(); --token_source.level; write(); }
}

void ClassOrInterfaceBodyDeclaration(boolean isInterface):
{
   boolean isNestedInterface = false;
   int modifiers;
}
{
  LOOKAHEAD(2)
  Initializer()
  {
     if (isInterface)
        throw new ParseException("An interface cannot have initializers");
  }
|
  modifiers = Modifiers() // Just get all the modifiers out of the way. If you want to do
              // more checks, pass the modifiers down to the member
  (
      ClassOrInterfaceDeclaration(modifiers)
    |
      EnumDeclaration(modifiers)
    |
      LOOKAHEAD( [ TypeParameters() ] <IDENTIFIER> "(" )
      ConstructorDeclaration(modifiers)
    |
      LOOKAHEAD( Type() <IDENTIFIER> ( "[" "]" )* ( "," | "=" | ";" ) )
      FieldDeclaration(modifiers)
    |
      MethodDeclaration(modifiers)
  )
|
  ";" { add(";"); write(); }
}

void FieldDeclaration(int modifiers):
{
    add(ModifierSet.toString(modifiers) + " ");
}
{
  // Modifiers are already matched in the caller
  Type() VariableDeclarator() ( "," { add(", "); } VariableDeclarator() )* ";" { add(";"); write(); }
}

void VariableDeclarator():
{}
{
  VariableDeclaratorId() [ "=" { add(" = "); } VariableInitializer() ]
}

void VariableDeclaratorId():
{}
{
  t=<IDENTIFIER> { add(t); } ( "[" "]" { trim(); add("[] "); } )*
}

void VariableInitializer():
{}
{
  ArrayInitializer()
|
  Expression()
}

// TODO: try to capture line separators after commas.  Long lines of array initializers
// are often split out by hand over several lines for readability.
void ArrayInitializer():
{}
{
    "{" { addStartBracket(); } [ VariableInitializer() ( LOOKAHEAD(2) "," { add(", "); } VariableInitializer() )* ] [ "," { add(", "); } ] "}" { addEndBracket(); write(); }
}

void MethodDeclaration(int modifiers):
{
    add(ModifierSet.toString(modifiers) + " ");
}
{
  // Modifiers already matched in the caller!
  [ TypeParameters() ]
  ResultType()
  MethodDeclarator() [ "throws" { add("throws"); } NameList() ]
  ( Block() | ";" { add(";"); write(); } )
}

void MethodDeclarator():
{}
{
  t=<IDENTIFIER> { add(t); } FormalParameters() ( "[" "]" { trim(); add("[] "); } )*
}

void FormalParameters():
{
    boolean added = false;
}
{
    "(" { trimWhitespace(); trim(); add("( "); } [ FormalParameter() { added = true; } ( "," { add(", "); } FormalParameter() )* ] ")" { if (!added) trim(); add(" )"); }
}

void FormalParameter():
{}
{
  [ "final" { add("final "); } ] Type() [ "..." { trim(); add("..."); } ] VariableDeclaratorId()
}

void ConstructorDeclaration(int modifiers):
{
    add(ModifierSet.toString(modifiers) + " ");
}
{
  [ TypeParameters() ]
  t=<IDENTIFIER> { add(t); } FormalParameters() [ "throws" { add("throws"); } NameList() ]
  "{" { addStartBracket(); write(); ++token_source.level;}
    [ LOOKAHEAD(ExplicitConstructorInvocation()) ExplicitConstructorInvocation() ]
    ( BlockStatement() )*
  "}" { addEndBracket(); --token_source.level; write(); }
}

void ExplicitConstructorInvocation():
{}
{
  LOOKAHEAD("this" Arguments() ";")
  "this" { add("this"); } Arguments() ";" { add(";"); write(); }
|
  [ LOOKAHEAD(2) PrimaryExpression() "." ] "super" { add("super"); } Arguments() ";" { add(";"); write(); }
}

void Initializer():
{}
{
  [ "static" { add("static "); }] Block()
}


/*
 * Type, name and expression syntax follows.
 */

void Type():
{}
{
   LOOKAHEAD(2) ReferenceType()
 |
   PrimitiveType()
}

void ReferenceType():
{}
{
   PrimitiveType() ( LOOKAHEAD(2) "[" "]" { trim(); add("[] "); } )+
  |
   ( ClassOrInterfaceType() ) ( LOOKAHEAD(2) "[" "]" { trim(); add("[] "); } )*
}

void ClassOrInterfaceType():
{}
{
  t=<IDENTIFIER> { add(t + " "); } [ LOOKAHEAD(2)  TypeArguments() ]
  ( LOOKAHEAD(2) "." { trim(); add("."); } t=<IDENTIFIER> { add(t + " "); } [ LOOKAHEAD(2) TypeArguments() ] )*
}

void TypeArguments():
{}
{
   "<" { trim(); add("<"); } TypeArgument() ( "," { trim(); add(", "); } TypeArgument() )* ">" { trim(); add("> "); }
}

void TypeArgument():
{}
{
   ReferenceType()
 |
   "?" { add("?"); } [ WildcardBounds() ]
}

void WildcardBounds():
{}
{
   "extends" { add("extends"); } ReferenceType()
 |
   "super" { add("super"); } ReferenceType()
}


void PrimitiveType():
{}
{
    (
  t="boolean"
|
  t="char"
|
  t="byte"
|
  t="short"
|
  t="int"
|
  t="long"
|
  t="float"
|
  t="double"
  )
    {
        add(t + " ");
    }
}

void ResultType():
{}
{
  "void" { add("void"); }
|
  Type()
}

void Name():
/*
 * A lookahead of 2 is required below since "Name" can be followed
 * by a ".*" when used in the context of an "ImportDeclaration".
 */
{}
{
    t=<IDENTIFIER> { add(t); }
  ( LOOKAHEAD(2) "." t=<IDENTIFIER> { add("."); add(t); }
  )*
}

void NameList():
{}
{
  Name() ( "," { add(", "); } Name() )*
}


/*
 * Expression syntax follows.
 */

void Expression():
/*
 * This expansion has been written this way instead of:
 *   Assignment() | ConditionalExpression()
 * for performance reasons.
 * However, it is a weakening of the grammar for it allows the LHS of
 * assignments to be any conditional expression whereas it can only be
 * a primary expression.  Consider adding a semantic predicate to work
 * around this.
 */
{}
{
  ConditionalExpression()
  [
    LOOKAHEAD(2)
    AssignmentOperator() Expression()
  ]
}

void AssignmentOperator():
{}
{
  (t="=" | t="*=" | t="/=" | t="%=" | t="+=" | t="-=" | t="<<=" | t=">>=" | t=">>>=" | t="&=" | t="^=" | t="|=")
  { add(" " + t.image + nbsp); }
}

void ConditionalExpression():
{}
{
    ConditionalOrExpression() [ "?" { add(" ?" + nbsp); } Expression() ":" { add(" :" + nbsp); } Expression() ]
}

void ConditionalOrExpression():
{}
{
  ConditionalAndExpression() ( "||" { add(" ||" + nbsp); } ConditionalAndExpression() )*
}

void ConditionalAndExpression():
{}
{
  InclusiveOrExpression() ( "&&" { add(" &&" + nbsp); } InclusiveOrExpression() )*
}

void InclusiveOrExpression():
{}
{
  ExclusiveOrExpression() ( "|" { add(" |" + nbsp); } ExclusiveOrExpression() )*
}

void ExclusiveOrExpression():
{}
{
  AndExpression() ( "^" { add(" ^" + nbsp); } AndExpression() )*
}

void AndExpression():
{}
{
  EqualityExpression() ( "&" { add(" &" + nbsp); } EqualityExpression() )*
}

void EqualityExpression():
{}
{
  InstanceOfExpression() ( ( "==" { add(" ==" + nbsp); } | "!=" { add(" !=" + nbsp); } ) InstanceOfExpression() )*
}

void InstanceOfExpression():
{}
{
  RelationalExpression() [ "instanceof" { add(" instanceof "); } Type() ]
}

void RelationalExpression():
{}
{
  ShiftExpression() ( ( "<" { add(" <" + nbsp); } | ">" { add(" >" + nbsp); } | "<=" { add(" <=" + nbsp); } | ">=" { add(" >=" + nbsp); } ) ShiftExpression() )*
}

void ShiftExpression():
{}
{
  AdditiveExpression() ( ( "<<" { add(" <<" + nbsp); } | RSIGNEDSHIFT() | RUNSIGNEDSHIFT() ) AdditiveExpression() )*
}

void AdditiveExpression():
{}
{
  MultiplicativeExpression() ( ( "+" { add(" +" + nbsp); } | "-" { add(" -" + nbsp); } ) MultiplicativeExpression() )*
}

void MultiplicativeExpression():
{}
{
  UnaryExpression() ( ( "*" { add(" *" + nbsp); } | "/" { add(" /" + nbsp); } | "%" { add(" %" + nbsp); } ) UnaryExpression() )*
}

void UnaryExpression():
{}
{
    // TODO: need to check for line separator following "+" and preserve it.
    // It is fairly common to break long strings into several lines concatenated
    // with +.  It's not good form, I know, but it is still done.
  ( "+" { add(" +"); } | "-" { add(" -"); } ) UnaryExpression()
|
  PreIncrementExpression()
|
  PreDecrementExpression()
|
  UnaryExpressionNotPlusMinus()
}

void PreIncrementExpression():
{}
{
  "++" { add(" ++"); } PrimaryExpression()
}

void PreDecrementExpression():
{}
{
  "--" { add(" --"); } PrimaryExpression()
}

void UnaryExpressionNotPlusMinus():
{}
{
  ( "~" { add(" ~"); } | "!" { add(" !"); } ) UnaryExpression()
|
  LOOKAHEAD( CastLookahead() )
  CastExpression()
|
  PostfixExpression()
}

// This production is to determine lookahead only.  The LOOKAHEAD specifications
// below are not used, but they are there just to indicate that we know about
// this. Nothing needs to be output from this production.
void CastLookahead():
{}
{
  LOOKAHEAD(2)
  "("  PrimitiveType()
|
  LOOKAHEAD("(" Type() "[")
  "("  Type() "[" "]" { trim(); add("[] "); }
|
  "("  Type() ")" ( "~" | "!" | "(" | <IDENTIFIER> | "this" | "super" | "new" | Literal() )
}

void PostfixExpression():
{}
{
  PrimaryExpression() [ "++" { trim(); add("++"); } | "--" { trim(); add("--"); } ]
}

void CastExpression():
{}
{
  LOOKAHEAD("(" PrimitiveType())
  "(" { add("( "); } Type() ")" { trim(); add(" ) "); } UnaryExpression()
|
  "(" { add("( "); } Type() ")" { trim(); add(" ) "); } UnaryExpressionNotPlusMinus()
}

void PrimaryExpression():
{}
{
  PrimaryPrefix() ( LOOKAHEAD(2) PrimarySuffix() )*
}

void MemberSelector():
{}
{
  "." { add("."); } TypeArguments() t=<IDENTIFIER> { add(t); }
}

void PrimaryPrefix():
{}
{
  Literal()
|
  "this" { add("this"); }
|
  "super" "." { add("super."); } t=<IDENTIFIER> { add(t); }
|
  "(" { add("( "); } Expression() ")" { add(" )"); }
|
  AllocationExpression()
|
  LOOKAHEAD( ResultType() "." "class" )
  ResultType() "." "class" { trim(); add(".class"); }
|
  Name()
}

void PrimarySuffix():
{}
{
  LOOKAHEAD(2)
  "." "this" { add(".this"); }
|
  LOOKAHEAD(2)
  "." { add("."); } AllocationExpression()
|
  LOOKAHEAD(3)
  MemberSelector()
|
  "[" { trim(); add("["); } Expression() "]" { trim(); add("]"); }
|
  "." { add("."); } t=<IDENTIFIER> { add(t); }
|
  Arguments()
}

void Literal():
{}
{
  t=<INTEGER_LITERAL> { trim(); add(t); }
|
  t=<FLOATING_POINT_LITERAL> { trim(); add(t); }
|
  t=<CHARACTER_LITERAL> { add(t); }
|
  t=<STRING_LITERAL> { add(t); }
|
  BooleanLiteral()
|
  NullLiteral()
}

void BooleanLiteral():
{}
{
  "true" { add("true"); }
|
  "false" { add("false"); }
}

void NullLiteral():
{}
{
  "null" { add("null"); }
}

void Arguments():
{
    boolean added = false;
}
{
    "(" { trimWhitespace(); trim(); add("( "); } [ ArgumentList() { added = true; } ] ")" { if (!added) trim(); add(" )"); }
}

void ArgumentList():
{}
{
  Expression() ( "," { add(", "); } Expression() )*
}

void AllocationExpression():
{}
{
  LOOKAHEAD(2)
  "new" { add("new "); } PrimitiveType() ArrayDimsAndInits()
|
  "new" { add("new "); } ClassOrInterfaceType() [ TypeArguments() ]
    (
      ArrayDimsAndInits()
    |
      Arguments() [ ClassOrInterfaceBody(false) ]
    )
}

/*
 * The third LOOKAHEAD specification below is to parse to PrimarySuffix
 * if there is an expression between the "[...]".
 */
void ArrayDimsAndInits():
{}
{
  LOOKAHEAD(2)
  ( LOOKAHEAD(2) "[" { trimWhitespace(); add("["); } Expression() "]" { trim(); add("]"); } )+ ( LOOKAHEAD(2) "[" "]" { trim(); add("[] "); } )*
|
  ( "[" "]" { trim(); add("[]"); } )+ ArrayInitializer()
}


/*
 * Statement syntax follows.
 */
void Statement():
{}
{
  LOOKAHEAD(2)
  LabeledStatement()
|
  AssertStatement()
|
  Block()
|
  EmptyStatement()
|
  StatementExpression() ";" { add(";"); write(); }
|
  SwitchStatement()
|
  IfStatement()
|
  WhileStatement()
|
  DoStatement()
|
  ForStatement()
|
  BreakStatement()
|
  ContinueStatement()
|
  ReturnStatement()
|
  ThrowStatement()
|
  SynchronizedStatement()
|
  TryStatement()
}


void AssertStatement():
{}
{
    "assert" { add("assert"); } Expression() [ ":" { add(":"); } Expression() ] ";" { add(";"); write(); }
}

void LabeledStatement():
{}
{
    t=<IDENTIFIER> { add(t); } ":" { add(":"); } Statement()
}

void Block():
{
}
{
    "{" { trimWhitespace(); addStartBracket(); write(); ++token_source.level; }
        ( BlockStatement() )*
    "}" { addEndBracket(); --token_source.level; write(); }    // TODO: don't always want to do this, for example with an anonymous inner class
}

void BlockStatement():
{}
{
  LOOKAHEAD([ "final" ] Type() <IDENTIFIER>)
  LocalVariableDeclaration() ";" { add(";"); write(); }
|
  Statement()
|
  ClassOrInterfaceDeclaration(0)
}

void LocalVariableDeclaration():
{}
{
  [ "final" { add("final "); } ] Type() VariableDeclarator() ( "," { add(", "); } VariableDeclarator() )*
}

void EmptyStatement():
{}
{
  ";" 
  {
      // add(token_source.ls); add(token_source.ls);  write(); add(";"); write();
      add(";");
      write();
  }
}

void StatementExpression():
/*
 * The last expansion of this production accepts more than the legal
 * Java expansions for StatementExpression.  This expansion does not
 * use PostfixExpression for performance reasons.
 */
{}
{
  PreIncrementExpression()
|
  PreDecrementExpression()
|
  PrimaryExpression()
  [
    "++" { add("++"); }
  |
    "--" { add("--"); }
  |
    AssignmentOperator() Expression()
  ]
}

void SwitchStatement():
{}
{
    "switch" "(" { add("switch ( "); } Expression() ")" "{" { add(" ) "); addStartBracket(); write(); ++token_source.level; }
        ( SwitchLabel() { ++token_source.level;} ( BlockStatement() )* { --token_source.level;} )*
    "}" { addEndBracket(); --token_source.level; write(); }
}

void SwitchLabel():
{}
{
"case" { add("case"); } Expression() ":" { add(":"); write(); }
|
"default" ":" { add("default:"); write(); }
}

void IfStatement():
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if statement.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 */
{
    boolean no_block = false;
    Token t;
}
{
    "if" "("
    {
        add("if ( ");
    }
    Expression() ")"
    { 
        add(" ) ");
        
        // Sun's coding standard says there should always be a block following
        // an "if" statement, so add the brackets if they are missing
        if (!getToken(1).image.equals("{")) {
            trimWhitespace(); 
            trim(); 
            addStartBracket();
            writeln();
            ++token_source.level;
            no_block = true;
        }
    }
    Statement()
    {
        if (no_block) {
            writeln();
            --token_source.level;
            addEndBracket();
            write();
        }
        no_block = false;
    }

    [
        LOOKAHEAD(1) t="else"
        // DONE: need to check for comment preceding the 'else', otherwise the
        // 'else' gets appended to the end of the comment, which in the case of
        // an END_OF_LINE_COMMENT is very bad.
        { 
            Token st = t.specialToken;
            Token ct = null;    // comment token
            while (st != null && ct == null) {
                switch(st.kind) {
                    // skip over whitespace
                    case JavaParserConstants.LINE_ENDERS:
                    case JavaParserConstants.BLANK_LINES:
                    case JavaParserConstants.SPACES:
                    case JavaParserConstants.TABS:
                        st = st.specialToken;
                        continue;
                    // check for comment
                    case JavaParserConstants.END_OF_LINE_COMMENT:
                    case JavaParserConstants.JAVADOC_COMMENT:
                    case JavaParserConstants.BLOCK_COMMENT:
                        ct = st;
                        break;
                    default:
                        continue;
                }
            }
            trimWhitespace(); 
            trim(); 
            if (ct != null || bracketStyle == BROKEN) {
                // have comment token preceding 'else', so start 'else' on new line
                writeln();
                add("else ");
            }
            else {
                add(" else "); 
            }
        }
        {
            if (getToken(1).image.equals("if")) {
                return;   
            }
            // Sun's coding standard says there should always be a block following
            // an "else" statement, so add the brackets if they are missing
            if (!getToken(1).image.equals("{")) {
                // remove any excess whitespace between the 'else' and the '{'
                trimWhitespace();
                trim();
                addStartBracket();
                writeln();
                ++token_source.level;
                no_block = true;
            }
        }
        Statement()
        {
            if (no_block) {
                writeln();
                --token_source.level;
                addEndBracket();
                write();
            }
        }
    ]
}

void WhileStatement():
{
    boolean no_block = false;
}
{
    "while" "(" { add("while ( "); } Expression() ")" { add(" )"); }
    {
        // if the next token isn't a "{" (indicating the start of a block) or
        // a ";" (indicating an empty statement), add "{" to make sure there is
        // indeed a block written.
        if (!getToken(1).image.equals("{") && !getToken(1).image.equals(";")) {
            addStartBracket();
            writeln();
            ++token_source.level;
            no_block = true;
        }
    }
    Statement()
    {
        if (no_block) {
            writeln();
            --token_source.level;
            addEndBracket();
            writeln();
        }
    }
}

void DoStatement():
{}
{
  // adding a ^ to the front of 'while' to differentiate it from 'while' at the
  // start of a 'while' loop.  This will be stripped off later.
  "do" { add("do "); } Statement() "while" "(" { trimWhitespace(); add(" ^while ( "); } Expression() ")" ";" { add(" );"); write(); }
}

void ForStatement():
{
    boolean no_block = false;
}
{
  "for" "(" { add("for ( "); }

  (
      LOOKAHEAD(Type() <IDENTIFIER> ":")
      Type() t=<IDENTIFIER> { add(t); } ":" { add(" : "); } Expression()
    |
    [ ForInit() ] ";" { add("; "); } [ Expression() ] ";" { add("; "); } [ ForUpdate() ]
  )

  ")" { add(" )"); }
    {
        // if the next token isn't a "{" (indicating the start of a block) or
        // a ";" (indicating an empty statement), add "{" to make sure there is
        // indeed a block written.
        if (!getToken(1).image.equals("{") && !getToken(1).image.equals(";") ) {
            addStartBracket();
            writeln();
            ++token_source.level;
            no_block = true;
        }
    }
    Statement()
    {
        if (no_block) {
            writeln();
            --token_source.level;
            addEndBracket();
            writeln();
        }
    }
}

void ForInit():
{}
{
  LOOKAHEAD( [ "final" ] Type() <IDENTIFIER> )
  LocalVariableDeclaration()
|
  StatementExpressionList()
}

void StatementExpressionList():
{}
{
  StatementExpression() ( "," { add(", "); } StatementExpression() )*
}

void ForUpdate():
{}
{
  StatementExpressionList()
}

void BreakStatement():
{}
{
  "break" { add("break"); } [ t=<IDENTIFIER> { add(t); } ] ";" { add(";"); write(); }
}

void ContinueStatement():
{}
{
  "continue" { add("continue"); } [ t=<IDENTIFIER> { add(t); } ] ";" { add(";"); write(); }
}

void ReturnStatement():
{}
{
  "return" { add("return"); } [ Expression() ] ";" { add(";"); write(); }
}

void ThrowStatement():
{}
{
  "throw" { add("throw"); } Expression() ";" { add(";"); write(); }
}

void SynchronizedStatement():
{}
{
  "synchronized" "(" { add("synchronized ( "); } Expression() ")" { add(" )"); } Block()
}

void TryStatement():
/*
 * Semantic check required here to make sure that at least one
 * finally/catch is present.
 */
{}
{
  "try" { add("try "); } Block()
  ( "catch" "(" 
      { 
          // DONE: check for comment preceding the 'catch'. See 'else' for how
          // to handle this. Don't forget to deal with 'finally' also.
            Token st = t.specialToken;
            Token ct = null;    // comment token
            while (st != null && ct == null) {
                switch(st.kind) {
                    // skip over whitespace
                    case JavaParserConstants.LINE_ENDERS:
                    case JavaParserConstants.BLANK_LINES:
                    case JavaParserConstants.SPACES:
                    case JavaParserConstants.TABS:
                        st = st.specialToken;
                        continue;
                    // check for comment
                    case JavaParserConstants.END_OF_LINE_COMMENT:
                    case JavaParserConstants.JAVADOC_COMMENT:
                    case JavaParserConstants.BLOCK_COMMENT:
                        ct = st;
                        break;
                    default:
                        continue;
                }
            }
            trimWhitespace(); 
            trim(); 
            if (ct != null || bracketStyle == BROKEN) {
                // have comment token preceding 'catch', so start 'catch' on new line
                writeln();
                add("catch ( ");
            }
            else {
                add(" catch ( "); 
            }
      } 
      FormalParameter() ")" { add(" ) "); } Block() )*
  [ 
    "finally" 
        { 
            Token st = t.specialToken;
            Token ct = null;    // comment token
            while (st != null && ct == null) {
                switch(st.kind) {
                    // skip over whitespace
                    case JavaParserConstants.LINE_ENDERS:
                    case JavaParserConstants.BLANK_LINES:
                    case JavaParserConstants.SPACES:
                    case JavaParserConstants.TABS:
                        st = st.specialToken;
                        continue;
                    // check for comment
                    case JavaParserConstants.END_OF_LINE_COMMENT:
                    case JavaParserConstants.JAVADOC_COMMENT:
                    case JavaParserConstants.BLOCK_COMMENT:
                        ct = st;
                        break;
                    default:
                        continue;
                }
            }
            trimWhitespace(); 
            trim(); 
            if (ct != null || bracketStyle == BROKEN) {
                // have comment token preceding 'finally', so start 'finally' on new line
                writeln();
                add("finally ");
            }
            else {
                add(" finally"); 
            }
        } 
        Block() 
  ]
}

/* We use productions to match >>>, >> and > so that we can keep the
 * type declaration syntax with generics clean
 */

void RUNSIGNEDSHIFT():
{}
{
  ( LOOKAHEAD({ getToken(1).kind == GT &&
                ((Token.GTToken)getToken(1)).realKind == RUNSIGNEDSHIFT} )
      ">" ">" ">" { add(" >>> "); }
  )
}

void RSIGNEDSHIFT():
{}
{
  ( LOOKAHEAD({ getToken(1).kind == GT &&
                ((Token.GTToken)getToken(1)).realKind == RSIGNEDSHIFT} )
      ">" ">" { add(" >> "); }
  )
}

/* Annotation syntax follows. */

void Annotation():
{}
{
   LOOKAHEAD( "@" Name() "(" ( <IDENTIFIER> "=" | ")" ))
   NormalAnnotation()
 |
   LOOKAHEAD( "@" Name() "(" )
   SingleMemberAnnotation()
 |
   MarkerAnnotation() 
}

void NormalAnnotation():
{}
{
   "@" { add("@"); } Name() "(" { add("( "); } [ MemberValuePairs() ] ")" { trimWhitespace(); trim(); add(" )"); write();}
}

void MarkerAnnotation():
{}
{
    "@" { token_source.trimNL(); add("@"); } Name() {  writeln(); }
}

void SingleMemberAnnotation():
{}
{
  "@" { add("@"); } Name() "(" { add("( "); } MemberValue() ")" { add(" )"); write(); }
}

void MemberValuePairs():
{}
{
   MemberValuePair() ( "," { add(", "); } MemberValuePair() )*
}

void MemberValuePair():
{}
{
    t=<IDENTIFIER> { add(t); } "=" { add(" = "); } MemberValue()
}

void MemberValue():
{}
{
   Annotation()
 |
   MemberValueArrayInitializer()
 |
   ConditionalExpression()
}

void  MemberValueArrayInitializer():
{}
{
    "{" { addStartBracket(); } MemberValue() ( LOOKAHEAD(2) "," { add(", "); } MemberValue() )* [ "," { add(", "); } ] "}" { addEndBracket();  }
}


/* Annotation Types. */

void AnnotationTypeDeclaration(int modifiers):
{
    add(ModifierSet.toString(modifiers) + " ");
}
{
  "@" "interface" { add("@interface"); } t=<IDENTIFIER> { add(t); } AnnotationTypeBody()
}

void AnnotationTypeBody():
{}
{
    "{" { addStartBracket(); write(); ++token_source.level; } ( AnnotationTypeMemberDeclaration() )* "}" { --token_source.level; addEndBracket(); write(); }
}

void AnnotationTypeMemberDeclaration():
{
   int modifiers;
}
{
 modifiers = Modifiers()
 (
   LOOKAHEAD(Type() <IDENTIFIER> "(")
   Type() t=<IDENTIFIER> { add(t); } "(" ")" { add("()"); } [ DefaultValue() ] ";" { add(";"); write(); }
  |
   ClassOrInterfaceDeclaration(modifiers)
  |
   EnumDeclaration(modifiers)
  |
   AnnotationTypeDeclaration(modifiers)
  |
   FieldDeclaration(modifiers)
 )
 |
   ( ";" { add(";"); write(); } )
}

void DefaultValue():
{}
{
  "default" { add("default"); } MemberValue()
}
