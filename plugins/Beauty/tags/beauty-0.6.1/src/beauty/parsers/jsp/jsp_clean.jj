/* JSP Parser for PMD.
 * It supports supports more-or-less well written JSP files.
 * The JSP Document style is supported, except for inline DTD.
 * The JSP Page style (<% ... %>) is supported.
 * Java code is not parsed.
 * Script code inside <script> ... </script> is not parsed. The tag body is 
 * captured as a single token, which means the token image can be passed off to
 * a javascript beautifier.
 *
 */

options { 
	UNICODE_INPUT=true;
	IGNORE_CASE = true; 
	STATIC = false; 
} 

PARSER_BEGIN(JspParser)
package beauty.parsers.jsp;

/** 
 * JSP Parser for PMD.
 * @author Pieter, Application Engineers NV/SA, http://www.ae.be
 */
public class JspParser {

	/**
	 * Return the contents of a quote.
	 * @param quote String - starting and ending with " or '
	 * @return String a substring of quote: quote without the first and list
	 * character.
	 */
	private static String quoteContent(String quote) {
		return quote.substring(1, quote.length()-1);
	}
	
	/**
	 * Return the contents of a EL expression or a Value Binding expression.
	 * @param expression String - starting with ${ or #{ and ending with }
	 * @return String a substring of expression: expression without the first two and list
	 * characters.
	 */
	private static String expressionContent(String expression) {
		return expression.substring(2, expression.length()-1).trim();
	}
	
    public static void main(String args[]) {
        JspParser parser;
        if (args.length == 0) {
            System.out.println("Jsp Parser:  Reading from standard input . . .");
            parser = new JspParser(System.in);
        } else{
            if (args.length == 1) {
                System.out.println("Jsp Parser:  Reading from file " + args[0] + " . . .");
                try {
                    parser = new JspParser(new java.io.FileInputStream(args[0]));
                } catch (java.io.FileNotFoundException e) {
                    System.out.println("Jsp Parser:  File " + args[0] + " not found.");
                    return;
                }
            } else {
                System.out.println("Jsp Parser:  Usage is one of:");
                System.out.println("         java JspParser < inputfile");
                System.out.println("OR");
                System.out.println("         java JspParser inputfile");
                return;
            }
        }

        try {
            parser.CompilationUnit();
            System.out.println("Jsp Parser:  Java program parsed successfully.");
        } catch (ParseException e) {
            System.out.println(e.getMessage());
            System.out.println("Jsp Parser:  Encountered errors during parse.");
        }
    }
}

PARSER_END(JspParser)


/** ******************************************************************** */
/** *************************  JSP LEXICON  **************************** */
/** ******************************************************************** */


/* This JavaCC lexicon has the following states:
 * - StartTagState : this is the state entered after the "<" of a tag, until a
 *    non-whitespace is found.
 *    This is only for tags, not for xml-comments, declarations, etc.
 * - AfterTagState : this is the state entered after the closing ">" of a tag,
 *    or xml-comment or declaration, until some non-whitespace is found.
 * - CommentState : the state between "<!--" and "-->"
 * - DeclarationState : the state between "<?" or "<!" and ">"
 * - CDataState : the state between "<![DATA[" and "]]>"
 * - InTagState : the state when inside a tag
 * - AttrValueStatue : the state when starting an attribute value, before the starting single or double quote
 * - DocTypeState : the state when inside a doctype declaration
 * - ElExpressionState : the state when inside a ElExpression
 * - DocTypeState : inside a document type declaration
 * - DocTypeExternalIdState : inside an "external id" part of a dtd
 * - AttrValueBetweenSingleQuotesState : inside an attribute that is surrounded by single quotes (')
 * - AttrValueBetweenDoubleQuotesState : inside an attribute that is surrounded by double quotes (")
 * - JspDirectiveState : inside a JSP directive not yet reaching the attributes of the directive
 * - JspDirectiveAttributesState : inside the attributes part of a directive
 * - JspScriptletState : inside a scriptlet <% ... %>
 * - JspExpressionState : inside an expression <%= ... %>
 * - JspDeclarationState : inside a declaration <%! ... %>
 * - JspCommentState : inside a comment <%-- ... --%>
 * - HtmlScriptContentState : inside an HTML script <script> ... </script> or a <style> ... </style>
 */


<*> TOKEN :
{
  <#ALPHA_CHAR: [
       "\u0024",
       "\u0041"-"\u005a",
       "\u005f",
       "\u0061"-"\u007a",
       "\u00c0"-"\u00d6",
       "\u00d8"-"\u00f6",
       "\u00f8"-"\u00ff"
      ] >
| <#NUM_CHAR:   [
       "\u0030"-"\u0039"
      ] >
| <#ALPHANUM_CHAR: ( <ALPHA_CHAR> | <NUM_CHAR> ) >
| <#IDENTIFIER_CHAR: ( <ALPHANUM_CHAR> | [ "_", "-", ".", ":" ] ) >
| <#IDENTIFIER: <ALPHA_CHAR> (<IDENTIFIER_CHAR>)* >
| <#XMLNAME: (<ALPHA_CHAR> | "_" | ":") (<IDENTIFIER_CHAR>)* >
| <#QUOTED_STRING_NO_BREAKS: ( "'" ( ~["'", "\r", "\n"] )* "'" ) 
                    | ( "\"" ( ~["\"", "\r", "\n"] )* "\"" ) >
| <#QUOTED_STRING: ( "'" ( ~["'"] )* "'" ) | ( "\"" ( ~["\""] )* "\"" ) >
| <#WHITESPACE: ( " " | "\t" | "\n" | "\r" ) >
| <#NEWLINE:    ( "\r\n" | "\r" | "\n" ) >
| <#QUOTE:      ( "'" | "\"" )>
| <#NO_WHITESPACE_OR_LT_OR_DOLLAR: (~[" ", "\t", "\n", "\r", "<", "$"])>
| <#NO_LT_OR_DOLLAR: (~["<", "$"])>
| <#DOLLAR: ("$")>
| <#NO_OPENBRACE_OR_LT: (~["<", "{"]) >
| <#TEXT_IN_EL: (~["}", "'", "\""])+ >
| <#EL_ESCAPE: ("\\${" | "\\#{") >

	// anything but --%>
| <#NO_JSP_COMMENT_END: (~["-"] | "-" ~["-"] | "--" ~["%"] | "--%" ~[">"])+ > 

| <#NO_JSP_TAG_END: ( ~["%"] | ("%" ~[">"]) )+ >
}


<DEFAULT, JspDirectiveState, JspDirectiveAttributesState> SKIP :
{
  < (<WHITESPACE>)+ >
}

<AttrValueState, AfterTagState, InTagState> SPECIAL_TOKEN :
{
  < (<WHITESPACE>)+ >
}

<DEFAULT, AfterTagState> TOKEN : 
{
  <TAG_START:      			"<"       	> 	: StartTagState
| <ENDTAG_START:   			"</"      	> 	: StartTagState
| <COMMENT_START:  			"<!--"    	> 	: CommentState
| <DECL_START:     			"<?"	  	>	: StartTagState
| <DOCTYPE_DECL_START: 		"<!DOCTYPE" >	: DocTypeState
| <CDATA_START:    			"<![CDATA[" >   : CDataState
| <JSP_COMMENT_START:		"<%--" 		>   : JspCommentState
| <JSP_DECLARATION_START: 	"<%!" 		>   : JspDeclarationState
| <JSP_EXPRESSION_START: 	"<%=" 		>   : JspExpressionState
| <JSP_SCRIPTLET_START:		"<%" 		>	: JspScriptletState
| <JSP_DIRECTIVE_START:		"<%@"		> 	: JspDirectiveState
}

<AfterTagState> TOKEN :
{
	<EL_EXPRESSION: "${" (<QUOTED_STRING> | <TEXT_IN_EL>)* "}" >
|	<UNPARSED_TEXT: (<NO_LT_OR_DOLLAR>|<DOLLAR><NO_OPENBRACE_OR_LT>|<EL_ESCAPE>)+ >
}

<JspDirectiveState> TOKEN :
{
	<JSP_DIRECTIVE_NAME: <IDENTIFIER> > : JspDirectiveAttributesState
}

<JspDirectiveAttributesState> TOKEN :
{
	<JSP_DIRECTIVE_ATTRIBUTE_NAME: 		<IDENTIFIER> >
|	<JSP_DIRECTIVE_ATTRIBUTE_EQUALS: 	"=" >
|	<JSP_DIRECTIVE_ATTRIBUTE_VALUE: 	<QUOTED_STRING> >
|	<JSP_DIRECTIVE_END: 				"%>" > : AfterTagState
}

<JspScriptletState> TOKEN :
{
	<JSP_SCRIPTLET_END: "%>" > : AfterTagState
|	<JSP_SCRIPTLET: <NO_JSP_TAG_END> >
}

<JspExpressionState> TOKEN :
{
	<JSP_EXPRESSION_END: "%>" > : AfterTagState
|	<JSP_EXPRESSION: <NO_JSP_TAG_END> >
}

<JspDeclarationState> TOKEN :
{
	<JSP_DECLARATION_END: "%>" > : AfterTagState
|	<JSP_DECLARATION: <NO_JSP_TAG_END> >
}

<JspCommentState> TOKEN : 
{
	<JSP_COMMENT_END: "--%>" > : AfterTagState
|	<JSP_COMMENT_CONTENT: <NO_JSP_COMMENT_END> >
}

<DocTypeState, DocTypeExternalIdState> TOKEN : 
{
	<WHITESPACES: (<WHITESPACE>)+ >
}

<DocTypeState> TOKEN:
{
	<NAME: (<XMLNAME>) > : DocTypeExternalIdState
}

<DocTypeExternalIdState> TOKEN:
{
	<PUBLIC: "PUBLIC">
|	<SYSTEM: "SYSTEM">
|	<DOCTYPE_DECL_END: ">" > : AfterTagState
|	<QUOTED_LITERAL: (<QUOTED_STRING>) >
}


<CDataState> TOKEN :
{
	<UNPARSED: 		(~[])  >
  | <CDATA_END:		("]]>") > : AfterTagState
}

<StartTagState> TOKEN :
{
  <TAG_NAME: <IDENTIFIER> > : InTagState
| <LST_ERROR: ~[]>          : DEFAULT
}

<InTagState> TOKEN :
{
  <ATTR_NAME: <IDENTIFIER> >
| <TAG_END: ">" >                       : AfterTagState
| <DECL_END: ("?>" | "!>") >            : AfterTagState
| <TAG_SLASHEND: "/>" >                 : AfterTagState
| <ATTR_EQ: "=" >                       : AttrValueState
| <IN_TAG_ERROR: ~[]>
}

<AttrValueState> TOKEN :
{
	<SINGLE_QUOTE: "'"> : AttrValueBetweenSingleQuotesState
|	<DOUBLE_QUOTE: "\""> : AttrValueBetweenDoubleQuotesState
}

<AttrValueBetweenSingleQuotesState, AttrValueBetweenDoubleQuotesState> TOKEN:
{
	<EL_EXPRESSION_IN_ATTRIBUTE: "${" (<QUOTED_STRING> | <TEXT_IN_EL>)* "}" >
|	<VALUE_BINDING_IN_ATTRIBUTE: "#{" (<QUOTED_STRING> | <TEXT_IN_EL>)* "}" >
|	<JSP_EXPRESSION_IN_ATTRIBUTE: "<%=" <NO_JSP_TAG_END> "%>" >
}

<AttrValueBetweenSingleQuotesState> TOKEN :
{
	<ENDING_SINGLE_QUOTE: "'"> : InTagState
|	<UNPARSED_TEXT_NO_SINGLE_QUOTES: 
		( (~["$", "#", "'"]) | (["$", "#"] ~["{", "'"]) | <EL_ESCAPE> )+ >
|	<DOLLAR_OR_HASH_SINGLE_QUOTE: ["$", "#"] "'" > : InTagState
}

<AttrValueBetweenDoubleQuotesState> TOKEN :
{
	<ENDING_DOUBLE_QUOTE: "\""> : InTagState
|	<UNPARSED_TEXT_NO_DOUBLE_QUOTES: 
		( (~["$", "#", "\""]) | (["$", "#"] ~["{", "\""]) | <EL_ESCAPE> )+ >
|	<DOLLAR_OR_HASH_DOUBLE_QUOTE: ["$", "#"] "\"" > : InTagState
}

<CommentState> TOKEN : 
{
  < COMMENT_END:  ("--" (" ")* ">" | "->") > : AfterTagState 
| < COMMENT_TEXT: (~[]) >
}

<HtmlScriptContentState> TOKEN :
{
	<HTML_SCRIPT_CONTENT: (~[]) >
	| <HTML_SCRIPT_END_TAG : "</script" | "</Script" | "</SCRIPT">
	{
		// We've done a custom lookahead for the closing </script> and found it.
		// Put it back into the input stream, so it can be processed normally.
		input_stream.backup(8);
		image.setLength(image.length() - 8); // kill the "</script"
		matchedToken.image = image.toString();
	} : AfterTagState
}

<HtmlStyleContentState> TOKEN :
{
	<HTML_STYLE_CONTENT: (~[]) >
	| <HTML_STYLE_END_TAG : "</style" | "</Style" | "</STYLE">
	{
		// We've done a custom lookahead for the closing </style> and found it.
		// Put it back into the input stream, so it can be processed normally.
		input_stream.backup(7);
		image.setLength(image.length() - 7); // kill the "</style"
		matchedToken.image = image.toString();
	} : AfterTagState
}

/** ******************************************************************** */
/** *************************  JSP GRAMMAR  **************************** */
/** ******************************************************************** */

/**
 * Main entry point
 */
void CompilationUnit() :
{}
{
  Prolog()
  
  Content() <EOF> 
}

/**
 * The optional prolog of a JSP, including (xml) declarations and DTD.
 */
void Prolog() :
{}
{
	(
    	LOOKAHEAD( ( CommentTag() | JspComment() )* 	Declaration() )
    	( CommentTag() | JspComment() )*
	  	Declaration()
	)?
	
    (
    	LOOKAHEAD( ( CommentTag() | JspComment() )* 	DoctypeDeclaration() )
    	( CommentTag() | JspComment() )*
	    DoctypeDeclaration()
	)?
}

/**
 * Everything between a start-tag and the corresponding end-tag of an element.
 */
void Content() :
{}
{
	( Text() | ContentElementPossiblyWithText()	)
	( ContentElementPossiblyWithText() )*
}

/**
 * A single (non-text) element that can occur between a start- and end-tag of an element.
 * Possibly followed by text.
 */
void ContentElementPossiblyWithText() :
{}
{
	( 
		CommentTag() 
	  |	Element() 
	  | CData() 
	  | JspComment() 
	  | JspDeclaration() 
	  | JspExpression() 
	  | JspScriptlet()
	  | JspDirective()
	)
	( Text() )?
}

void JspDirective() :
{ }
{
	<JSP_DIRECTIVE_START>
	<JSP_DIRECTIVE_NAME> 
	
	(
		JspDirectiveAttribute()
	)*
	<JSP_DIRECTIVE_END>
}

void JspDirectiveAttribute() : 
{ }
{
	<JSP_DIRECTIVE_ATTRIBUTE_NAME> 
	<JSP_DIRECTIVE_ATTRIBUTE_EQUALS>
	<JSP_DIRECTIVE_ATTRIBUTE_VALUE>
}

void JspScriptlet() :
{ }
{
	<JSP_SCRIPTLET_START>
	<JSP_SCRIPTLET>
	<JSP_SCRIPTLET_END>
}

void JspExpression() :
{ }
{
	<JSP_EXPRESSION_START>
	<JSP_EXPRESSION>
	<JSP_EXPRESSION_END>
}

void JspDeclaration() :
{ }
{
	<JSP_DECLARATION_START>
	<JSP_DECLARATION>
	<JSP_DECLARATION_END>
}

void JspComment() :
{}
{
	<JSP_COMMENT_START>
	<JSP_COMMENT_CONTENT>
	<JSP_COMMENT_END>
}

/**
 * This production groups all characters between two tags, where
 * tag is an xml-tag "<...>" or a jsp-page-tag "<%...%>" or CDATA "<![CDATA[...]]>".
 * Text consists of unparsed text and/or Expression Language expressions.
 */
void Text() :
{
}
{
	(	UnparsedText()
	  |	ElExpression()
	)+
}

void UnparsedText() :
{ }
{
    <UNPARSED_TEXT>
}

/**
 * Text that contains no single quotes, and that does not contain the start
 * of a EL expression or value binding.
 */
void UnparsedTextNoSingleQuotes() : 
{ }
{
	<UNPARSED_TEXT_NO_SINGLE_QUOTES> 
}

/**
 * Text that contains no double quotes, and that does not contain the start
 * of a EL expression or value binding.
 */
void UnparsedTextNoDoubleQuotes() : 
{ }
{
	<UNPARSED_TEXT_NO_DOUBLE_QUOTES> 
}

/**
 * An EL expression, not within an attribute value.
 */
void ElExpression() :
{ }
{
	<EL_EXPRESSION> 
}

void ValueBindingInAttribute() :
{ }
{
	<VALUE_BINDING_IN_ATTRIBUTE> 
}

void ElExpressionInAttribute() :
{ }
{
	<EL_EXPRESSION_IN_ATTRIBUTE> 
}

void CData() :
{
}
{
	<CDATA_START> ( <UNPARSED>  )* <CDATA_END>
}

/**
 * A XML element, either with a single empty tag, or with a starting and closing tag
 * with optional contained content.
 */
void Element() :
{
	Token startTagName;
	Token endTagName;
	String tagName;
}
{
    <TAG_START> 
    startTagName = <TAG_NAME> { tagName = startTagName.image; }
    (Attribute())*
    ( 
    	(<TAG_END> 
    		{

		    	// Content in a <script> element needs special treatment (like a comment or CDataSection).
		    	// Tell the TokenManager to start looking for the body of a script element.  In this
		    	// state all text will be consumed by the next token up to the closing </script> tag.
		    	// This is a context sensitive switch for the token manager, not something one can
		    	// express using normal JavaCC syntax.  Hence the hoop jumping.
				if ("script".equalsIgnoreCase(startTagName.image)) {
					token_source.SwitchTo(HtmlScriptContentState);
				}
				if ("style".equalsIgnoreCase(startTagName.image)) {
					token_source.SwitchTo(HtmlStyleContentState);
				}
    		}
    	
   		(HtmlScript() | HtmlStyle() | Content())?
    	
    	<ENDTAG_START> 
    	endTagName = <TAG_NAME> 
    		{ 
    			if (! tagName.equalsIgnoreCase(endTagName.image)) {
    			    // throw exception
    			}
    		}
    	<TAG_END>)
    	
     	| 
     	
     	(<TAG_SLASHEND> )
    )
}

void Attribute() : 
{	}
{
  <ATTR_NAME>
  (
  	<ATTR_EQ> 
  	AttributeValue()
  )
}

/**
 * The value of an attribute of an element.
 * EL expressions, JSF value bindings, and JSP expressions
 * are parsed as sub-nodes of the AttributeValue node.
 */
void AttributeValue() : 
{
}
{
  	(
	  	( 	<DOUBLE_QUOTE>
		  	( (	UnparsedTextNoDoubleQuotes() 
			  |	QuoteIndependentAttributeValueContent()
			) )*
			( 	<ENDING_DOUBLE_QUOTE> 
			  |	<DOLLAR_OR_HASH_DOUBLE_QUOTE>
			) 
		)
		|
	  	( 	<SINGLE_QUOTE>
		  	( ( UnparsedTextNoSingleQuotes()
			  |	QuoteIndependentAttributeValueContent()
			) )*
			( 	<ENDING_SINGLE_QUOTE> 
			  | <DOLLAR_OR_HASH_SINGLE_QUOTE>
			 ) 
		)
	)
}

/**
 * Partial content of an attribute value that can contain all quotes.
 * This groups EL expressions, value bindings, and JSP expressions.
 */
void QuoteIndependentAttributeValueContent() :
{ }
{
	(	ElExpressionInAttribute()
	| 	ValueBindingInAttribute()
  	| 	JspExpressionInAttribute()
	)
}

void JspExpressionInAttribute() : 
{ }
{
	<JSP_EXPRESSION_IN_ATTRIBUTE> 
}

void CommentTag() :
{
}
{
  <COMMENT_START>
  ( <COMMENT_TEXT> )* 
  <COMMENT_END>
}
  
void Declaration() :
{ }
{
  <DECL_START> 
  (Attribute())* 
  <DECL_END>
}

void DoctypeDeclaration() :
{ }
{
	<DOCTYPE_DECL_START>
	 <WHITESPACES>
	 <NAME>
	 (<WHITESPACES>)?
	 (DoctypeExternalId() 	 (<WHITESPACES>)?)? 
 	<DOCTYPE_DECL_END>
}

void DoctypeExternalId() :
{
}
{
		(	<SYSTEM>
			<WHITESPACES> 
			<QUOTED_LITERAL> 
		)
	| 	
		(	<PUBLIC>
			<WHITESPACES> 
			<QUOTED_LITERAL> 
			<WHITESPACES> 
			<QUOTED_LITERAL> 
		)
}

void HtmlScript() :
{
}
{
	(<HTML_SCRIPT_CONTENT> )*
	<HTML_SCRIPT_END_TAG>
}

void HtmlStyle() :
{
}
{
	(<HTML_STYLE_CONTENT> )*
	<HTML_STYLE_END_TAG>
}
