/*
 * jEdit - Programmer's Text Editor
 * :tabSize=8:indentSize=8:noTabs=false:
 * :folding=explicit:collapseFolds=1:
 *
 * Copyright Â© 2011 Matthieu Casanova
 *
 * This program is free software; you can redistribute it and/or
 * modify it under the terms of the GNU General Public License
 * as published by the Free Software Foundation; either version 2
 * of the License, or any later version.
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program; if not, write to the Free Software
 * Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
 */

options {
  STATIC = false;
}

PARSER_BEGIN(HTMLZenParser)


package superabbrevs.zencoding.html;

import superabbrevs.zencoding.ZenParser;
import java.util.*;
import java.text.MessageFormat;

public class HTMLZenParser implements ZenParser
{
	private int index = 1;

	//{{{ getTabs() method
	private String getTabs(int indent)
	{
		StringBuilder builder = new StringBuilder();
		for (int i = 0;i<indent;i++)
		{
			builder.append('\t');
		}
		return builder.toString();
	} //}}}

	private String pad(int i, int num)
	{
		String t = Integer.toString(i);
		while (t.length() < num)
		{
			t = '0' + t;
		}
		return t;
	}

	//{{{ merge() method
	private void merge(LinkedList<String> begin, LinkedList<String> middle, LinkedList<String> end, StringBuilder builder)
	{
		if (builder.length() != 0)
			builder.append('\n');
		for (int i = 0;i<begin.size();i++)
		{
			if (i != 0)
			{
				builder.append(getTabs(i));
			}
			builder.append(begin.get(i));
			builder.append("\n");
		}
		for (int i = 0;i<middle.size();i++)
		{
			builder.append(getTabs(begin.size()));
			builder.append(middle.get(i));
			if (i < middle.size() - 1)
			{
				builder.append("\n");
			}
			else if (!end.isEmpty())
			{
				builder.append("\n").append(getTabs(begin.size() - 1));
			}
		}
		for (int i = end.size() - 1;i>= 0;i--)
		{
			builder.append(end.get(i));
			if (i != 0)
			{
				builder.append("\n").append(getTabs(i-1));
			}
		}
		middle.clear();
	} //}}}
}

PARSER_END(HTMLZenParser)

//{{{ Tokens
SKIP:
{
    " "
  | "\t"
  | "\n"
  | "\r"
}

TOKEN:
{
	<SHARP: "#">
|
	<DOT: ".">
|
	<GT: ">">
|
	<PLUS: "+">
|
	<EQUALS: "=">
|
	<LBRACKET: "[">
|
	<RBRACKET: "]">
}

TOKEN:
{
	<INTEGER_LITERAL: (<DIGIT>)+ >
|
	<IDENTIFIER:(<LETTER>)+>
|
	<ID_MULTIPLIER: ("$")+ "*" <INTEGER_LITERAL>>
|
	<MULTIPLIER: "*" <INTEGER_LITERAL>>
|
	<LITERAL:
	      "\""
	      (   (~["\"","\\","\n","\r"]) | ("\\" ~[] ) )*
	      "\""
	  >
|
	<#LETTER: ["A"-"Z","_","-","a"-"z"]>
|
	<#DIGIT: ["0"-"9"]>
} //}}}

//{{{ parse() method
String parse():
{
	LinkedList<String> begin = new LinkedList<String>();
	LinkedList<String> end = new LinkedList<String>();
	LinkedList<String> middle = new LinkedList<String>();
	StringBuilder builder = new StringBuilder();
}
{
	tag(begin,middle, end, builder)
	{
		merge(begin, middle, end, builder);
		return builder.toString();
	}
} //}}}

//{{{ tag() method
void tag(LinkedList<String> begin, LinkedList<String> middle, LinkedList<String> end, StringBuilder b):
{
	String temp;
	boolean finalTag = true;
	String tagName = "div";
}
{
	[ <IDENTIFIER> { tagName = token.image; } ]
	{
		begin.add('<'+tagName);
		end.add("</"+tagName+'>');
	}
	//{{{ #id
	[
		temp = id()
		{
			String last = begin.removeLast();
			begin.add(last + temp);
		}
	] //}}}
	//{{{ .class.class
	[
		temp = cssClass()
		{
			String last = begin.removeLast();
			begin.add(last + temp);
		}
	] //}}}
	//{{{ [attributes]
	[
		temp = attributes()
		{
			String last = begin.removeLast();
			begin.add(last + temp);
		}
	] //}}}
	[
		multiplier(begin, middle, end)
		{
			finalTag = false;
		}
	]
	//{{{ >
	[
		<GT>
		{
			finalTag = false;
			temp = begin.removeLast();
			begin.add(temp + ">");
		}
		tag(begin, middle, end, b)
	] //}}}
	{
		if (finalTag)
		{
			temp = begin.removeLast();
			middle.add(temp + ">$" + index++ + end.removeLast());
		}
	}
	//{{{ +tag
	[
		LOOKAHEAD(2)
		<PLUS>
		// {
		// 	merge(begin, middle, end, b);
		// }
		tag(begin, middle, end, b)
	] //}}}
	//{{{ *int
	/*[
		LOOKAHEAD(<STAR> <INTEGER_LITERAL>)
		<STAR>
		<INTEGER_LITERAL>
		{
			String mid = middle.getLast();
			MessageFormat format = new MessageFormat("{0}${1}<{2}");
			String _s = "";
			String _e = "";
			try
			{
				Object[] parse = format.parse(mid);
				_s = parse[0].toString() + '$';
				_e = '<' + parse[2].toString();
			}
			catch (java.text.ParseException e)
			{
				e.printStackTrace();
			}
			int count = Integer.parseInt(token.image);
                        for (int i = 0;i<count-1;i++)
                        {
                                middle.add(_s + index++ + _e);
                        }
		}
	]*/ //}}}
} //}}}

//{{{ multiplier() method
void multiplier(LinkedList<String> begin, LinkedList<String> middle, LinkedList<String> end):
{
	Token multiplier;
}
{
	(
		multiplier = <ID_MULTIPLIER>
	|
		multiplier = <MULTIPLIER>
	)
	{
		String[] split = multiplier.image.split("\\*");
		int count = Integer.parseInt(split[1]);
		String _s = begin.removeLast();
		String _e = end.removeLast();
		int pad = split[0].length();
		if (pad != 0)
		{
			_s = _s.substring(0, _s.length()-1);
			for (int i = 0;i<count;i++)
			{
				String id = pad(i+1, pad);
				middle.add(_s + id + "\">$" + index++ + _e);
			}
		}
		else
		{
			for (int i = 0;i<count;i++)
			{
				middle.add(_s + ">$" + index++ + _e);
			}
		}
	}
} //}}}

//{{{ id() method
String id():
{
}
{
	<SHARP>
	<IDENTIFIER>
	{
		return " id=\""+token.image+"\"";
	}
} //}}}

//{{{ cssClass() method
String cssClass():
{
	String ret = "";
}
{
	<DOT>
	<IDENTIFIER>
	{
		ret += " " + token.image;
	}
	(
		<DOT>
		<IDENTIFIER>
		{
			ret += " " + token.image;
		}
	)*
	{
		return " class=\""+ret.trim()+"\"";
	}
} //}}}

//{{{ cssClass() method
String attributes():
{
	String ret = "";
	boolean shouldAddDoublequote;
}
{
	<LBRACKET>
	(
		{
			shouldAddDoublequote = true;
		}
		<IDENTIFIER> { ret += ' ' + token.image + '='; }
		[
			<EQUALS>
			{
				shouldAddDoublequote = false;
			}
			(
					<IDENTIFIER> { ret += '"' + token.image + '"'; }
				|
					<INTEGER_LITERAL> { ret += '"' + token.image + '"'; }
				|
					<LITERAL> { ret += token.image; }
			)
		]
		{
			if (shouldAddDoublequote)
				ret += "\"$"+index++ +"\"";
		}

	)*
	<RBRACKET>
	{
		return ret;
	}
} //}}}







